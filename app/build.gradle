plugins {
    id 'com.github.triplet.play' version '3.0.0'
}

apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.crashlytics'

def isTravis = System.getenv("CI") == "true"
android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.gianlu.aria2app"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 215
        versionName "5.9.3"
    }

    if (isTravis) {
        signingConfigs {
            release {
                storeFile file("../.travis/keystore.jks")
                storePassword System.getenv("keystore_storePsswd")
                keyAlias = System.getenv("keystore_keyAlias")
                keyPassword System.getenv("keystore_keyPsswd")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (isTravis) signingConfig signingConfigs.release
        }
    }

    flavorDimensions 'main'
    productFlavors {
        standard {
            dimension 'main'

            buildTypes {
                release {
                    minifyEnabled true
                    shrinkResources true
                }
            }
        }

        foss {
            dimension 'main'

            buildTypes {
                release {
                    minifyEnabled false
                    shrinkResources false
                }
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        warning 'MissingTranslation'
    }

    playConfigs {
        foss {
            commit.set(false)
            enabled.set(false)
        }
    }
}

play {
    defaultToAppBundles.set(true)
    enabled.set(isTravis)
    serviceAccountCredentials.set(file("../.travis/secrets.json"))
    track.set("production")
    releaseStatus.set(com.github.triplet.gradle.androidpublisher.ReleaseStatus.DRAFT)
}

dependencies {
    implementation project(':CommonUtils')
    implementation project(':aria2lib')

    implementation 'androidx.tonyodev.fetch2:xfetch2:3.1.5'
    implementation 'androidx.tonyodev.fetch2okhttp:xfetch2okhttp:3.1.5'

    implementation 'commons-net:commons-net:3.7.2'
    implementation 'com.hierynomus:smbj:0.10.0'
    implementation 'com.jcraft:jsch:0.1.55'

    implementation 'com.squareup.okhttp3:okhttp:4.9.0'

    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'
    implementation 'com.getbase:floatingactionbutton:1.10.1'
    implementation 'org.apmem.tools:layouts:1.10@aar'
    implementation 'com.github.devgianlu:Android--Library-Country-Flags:6fc6059f91'

    implementation 'androidx.documentfile:documentfile:1.0.1'
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.1.0-alpha01'

    implementation 'com.google.android.material:material:1.3.0-alpha04'
}

task printVersionName {
    doLast {
        println android.defaultConfig.versionName
    }
}

apply plugin: 'com.google.gms.google-services'