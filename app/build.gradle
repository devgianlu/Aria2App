plugins {
    id 'com.github.triplet.play' version '3.0.0'
}

apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.crashlytics'

def isCi = System.getenv("CI") == "true"
android {
    compileSdkVersion 30
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.gianlu.aria2app"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 217
        versionName "5.9.5"
    }

    if (isCi) {
        signingConfigs {
            release {
                storeFile file("../.gtihub/keystore.jks")
                storePassword System.getenv("KEYSTORE_STORE_PASS")
                keyAlias = System.getenv("KEYSTORE_KEY_ALIAS")
                keyPassword System.getenv("KEYSTORE_KEY_PASS")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (isCi) signingConfig signingConfigs.release
        }
    }

    flavorDimensions 'main'
    productFlavors {
        standard {
            dimension 'main'

            buildTypes {
                release {
                    minifyEnabled true
                    shrinkResources true
                }
            }
        }

        foss {
            dimension 'main'

            buildTypes {
                release {
                    minifyEnabled false
                    shrinkResources false
                }
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        warning 'MissingTranslation'
    }

    playConfigs {
        foss {
            commit.set(false)
            enabled.set(false)
        }
    }
}

play {
    defaultToAppBundles.set(true)
    enabled.set(isCi)
    serviceAccountCredentials.set(file("../.github/secrets.json"))
    track.set("production")
    releaseStatus.set(com.github.triplet.gradle.androidpublisher.ReleaseStatus.DRAFT)
}

dependencies {
    implementation project(':CommonUtils')
    implementation project(':aria2lib')

    implementation 'androidx.tonyodev.fetch2:xfetch2:3.1.6'
    implementation 'androidx.tonyodev.fetch2okhttp:xfetch2okhttp:3.1.6'

    implementation 'commons-net:commons-net:3.7.2'
    implementation 'com.hierynomus:smbj:0.10.0'
    implementation 'com.jcraft:jsch:0.1.55'

    implementation 'com.squareup.okhttp3:okhttp:4.9.1'

    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'
    implementation 'com.getbase:floatingactionbutton:1.10.1'
    implementation 'org.apmem.tools:layouts:1.10@aar'
    implementation 'com.github.devgianlu:Android--Library-Country-Flags:6fc6059f91'

    implementation 'androidx.documentfile:documentfile:1.0.1'
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.1.0-alpha01'

    implementation 'com.google.android.material:material:1.3.0'
}

task printVersionName {
    doLast {
        println android.defaultConfig.versionName
    }
}

apply plugin: 'com.google.gms.google-services'