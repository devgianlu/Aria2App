plugins {
    id 'com.github.triplet.play' version '2.2.1'
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

def isTravis = System.getenv("CI") == "true"
android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "com.gianlu.aria2app"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 183
        versionName "5.2.0"
    }

    if (isTravis) {
        signingConfigs {
            release {
                storeFile file("../.travis/keystore.jks")
                storePassword System.getenv("keystore_storePsswd")
                keyAlias = System.getenv("keystore_keyAlias")
                keyPassword System.getenv("keystore_keyPsswd")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (isTravis) signingConfig signingConfigs.release
        }
    }

    flavorDimensions 'main'
    productFlavors {
        standard {
            dimension 'main'
        }
        foss {
            dimension 'main'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        warning 'MissingTranslation'
    }

    playConfigs {
        foss {
            commit = false
            enabled = false
        }
    }
}

play {
    enabled = isTravis
    serviceAccountCredentials = file("../.travis/secrets.json")
    track = "production"
    releaseStatus = "draft"
}

dependencies {
    api project(':CommonUtils')
    api project(':CountryFlags')
    api project(':aria2lib')
    api 'androidx.tonyodev.fetch2:xfetch2:3.1.4'
    api 'androidx.tonyodev.fetch2okhttp:xfetch2okhttp:3.1.4'
    api 'com.squareup.okhttp3:okhttp:4.0.1'
    api 'com.github.PhilJay:MPAndroidChart:v3.1.0'
    api 'com.getbase:floatingactionbutton:1.10.1'
    api 'org.apmem.tools:layouts:1.10@aar'
}

task printVersionName {
    doLast {
        println android.defaultConfig.versionName
    }
}